/*******************************************************************************
 * Copyright (c) 2014 Vienna University of Technology.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 * Martin Fleck (Vienna University of Technology) - initial API and implementation
 *
 * Initially developed in the context of ARTIST EU project www.artist-project.eu
 *******************************************************************************/
grammar eu.artist.postmigration.nfrvt.lang.tsl.TSL with eu.artist.postmigration.nfrvt.lang.common.ARTISTCommon

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.artist.eu/postmigration/nfrvt/lang/nsl/NSL" as nsl
import "http://www.artist.eu/postmigration/nfrvt/lang/common/ARTISTCommon" as common

generate tsl "http://www.artist.eu/postmigration/nfrvt/lang/tsl/TSL"

ARTISTModel returns common::ARTISTModel:
	PatternCatalogue
;

PatternCatalogue:
	imports += ImportNamespace*
	"pattern-catalogue" name = QualifiedName "{"
		(patterns += Pattern)*
	"}"
;

Pattern:
	"pattern" name = ID "{"
		"context" context = STRING ","
		"problem" problem = STRING ","
		"solution" solution = STRING (","
		"property-impacts" "["
			propertyImpacts += PropertyImpact ("," propertyImpacts += PropertyImpact)*
		"]")? (","
		"pattern-impacts" "["
			patternImpacts += PatternImpact ("," patternImpacts += PatternImpact)*
		"]")? (","
		"related-patterns" "["
			relatedPatterns += [Pattern | QualifiedName] ("," relatedPatterns += [Pattern | QualifiedName])*
		"]")?
	"}"
;

PatternImpact:
	target = [Pattern | QualifiedName] 
	impact = Impact
;

PropertyImpact returns nsl::PropertyImpact:
	target = [nsl::Property | QualifiedName] 
	impact = Impact
;